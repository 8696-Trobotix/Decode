//
// build.gradle in TeamCode
//
// Most of the definitions for building your module reside in a common, shared
// file 'build.common.gradle'. Being factored in this way makes it easier to
// integrate updates to the FTC into your code. If you really need to customize
// the build definitions, you can place those customizations in this file, but
// please think carefully as to whether such customizations are really necessary
// before doing so.


// Custom definitions may go here

// Include common definitions from above.
buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://repo.dairy.foundation/releases"
        }
    }
    dependencies {
        classpath "dev.frozenmilk:Load:0.2.4"
    }
}

plugins {
    id "com.diffplug.spotless" version "7.2.1"
    id "com.peterabeles.gversion" version "1.10.3"
}

apply from: '../build.common.gradle'
apply from: '../build.dependencies.gradle'

android {
    namespace = 'org.firstinspires.ftc.teamcode'

    packagingOptions {
        jniLibs.useLegacyPackaging true
    }
}

dependencies {
    implementation project(':FtcRobotController')
    implementation("dev.frozenmilk.sinister:Sloth:0.2.4")
}

// Android tasks are generated during configuration, so we wrap this in afterEvaluate {}
afterEvaluate {
    tasks.named("compileDebugJavaWithJavac").configure {
        dependsOn("createVersionFile", "spotlessApply")
    }
}

gversion {
    srcDir       = "src/main/java/"
    classPackage = "org.firstinspires.ftc.teamcode"
    className    = "BuildConstants"
    dateFormat   = "yyyy-MM-dd HH:mm:ss z"
    timeZone     = "UTC" // Use preferred time zone
    indent       = "  "
}

spotless {
    java {
        target fileTree(".") {
            include "**/*.java"
            exclude "**/build/**", "**/build-*/**", "FtcRobotController/**"
        }
        removeUnusedImports()
        importOrder()
        cleanthat()
        googleJavaFormat()
        endWithNewline()
        licenseHeader("// Copyright (c) 2025-2026 FTC 8696\n// All rights reserved.\n\n")
    }
    groovyGradle {
        target fileTree(".") {
            include "**/*.gradle"
            exclude "**/build/**", "**/build-*/**"
        }
        greclipse()
        leadingTabsToSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
    }
    format "misc", {
        target fileTree(".") {
            include "**/*.md", "**/.gitignore"
            exclude "**/build/**", "**/build-*/**", "FtcRobotController/**"
        }
        trimTrailingWhitespace()
        leadingTabsToSpaces(4)
        endWithNewline()
    }
}
